VPRJAQ ;SLC/KCM -- Query for JSON objects across patients
 ;;1.0;JSON DATA STORE;;Sep 01, 2012
 ;
QCOUNT(CNTNM) ; Return a set of counts across patients
 ; return tallies as data:{items:[{"topic":"med","count":4}
 I '$L(CNTNM) D SETERROR^VPRJRER(101) Q
 N BUFFER S BUFFER=""
 K ^TMP($J)
 ;
 N TOPIC,DATA,COUNT,X
 S DATA=0,TOPIC=""
 F  S TOPIC=$O(^VPRPTX("count",CNTNM,TOPIC)) Q:TOPIC=""  D
 . S COUNT=+^VPRPTX("count",CNTNM,TOPIC)
 . S X=$S('DATA:"",1:",")_"{""topic"":"""_TOPIC_""",""count"":"_COUNT_"}"
 . S DATA=DATA+1,DATA(DATA)=X
 S X=$$BLDHEAD^VPRJCB(DATA) D STAGE^VPRJCB(X)
 S DATA=0 F  S DATA=$O(DATA(DATA)) Q:'DATA  D STAGE^VPRJCB(DATA(DATA))
 D STAGE^VPRJCB("]}}"),OUT^VPRJCB
 Q
QINDEX(INDEX,RANGE,ORDER,BAIL,TEMPLATE,FILTER) ; Query across patients by index
 I $G(INDEX)="pid",($G(TEMPLATE)="pid") D QPID Q
 ;
 I '$L($G(INDEX)) D SETERROR^VPRJRER(101) Q
 N VPRDATA,METHOD,CLAUSES
 S RANGE=$G(RANGE),ORDER=$G(ORDER),BAIL=$G(BAIL),TEMPLATE=$G(TEMPLATE),FILTER=$G(FILTER)
 S VPRDATA=0 S:'BAIL BAIL=999999
 M INDEX=^VPRMETA("index",INDEX,"common")
 S METHOD=$G(INDEX("method")) I '$L(METHOD) D SETERROR^VPRJRER(102,INDEX) Q
 I $L(FILTER) D PARSE^VPRJCF(FILTER,.CLAUSES) Q:$G(HTTPERR)
 D SETORDER^VPRJCO(.ORDER) Q:$G(HTTPERR)
 K ^TMP("VPRDATA",$J)
 D QATTR^VPRJAQA
 D BUILD^VPRJCB
 K ^TMP("VPRDATA",$J)
 Q
QPID ; Custom query to just return all PID's (/vpr/all/index/pid/pid)
 K ^TMP($J)
 N COUNT,PID,LINE,BUFFER
 S COUNT=0,LINE=2,PID="",BUFFER=""
 F  S PID=$O(^VPRPT(PID)) Q:'$L(PID)  D
 . S COUNT=COUNT+1
 . I $L(BUFFER)>4000 S ^TMP($J,LINE)=BUFFER,LINE=LINE+1,BUFFER=""
 . S BUFFER=BUFFER_$S(COUNT>1:",",1:"")_""""_PID_""""
 S ^TMP($J,LINE)=BUFFER_"]}}"
 S ^TMP($J,1)=$$BLDHEAD^VPRJCB(COUNT)
 Q
QFIND(COLL,ORDER,BAIL,TEMPLATE,FILTER) ; Query across patients using filter criteria
 N VPRDATA,CLAUSES,PREFIX,PID,KEY
 S ORDER=$G(ORDER),BAIL=$G(BAIL),TEMPLATE=$G(TEMPLATE),FILTER=$G(FILTER)
 S VPRDATA=0 S:'BAIL BAIL=999999
 I $L(FILTER) D PARSE^VPRJCF(FILTER,.CLAUSES) Q:$G(HTTPERR)
 D SETORDER^VPRJCO(.ORDER) Q:$G(HTTPERR)
 K ^TMP("VPRDATA",$J)
 S PID="" F  S PID=$O(^VPRPT(PID)) Q:'$L(PID)  D
 . S PREFIX="urn:va:"_COLL_":",KEY=PREFIX
 . F  S KEY=$O(^VPRPT(PID,KEY)) Q:$E(KEY,1,$L(PREFIX))'=PREFIX  D ADDONE^VPRJPQA(KEY,0)
 D BUILD^VPRJCB
 K ^TMP("VPRDATA",$J)
 Q
